# ----------------------------------------------------
# This file is generated by the Qt Visual Studio Add-in.
# ------------------------------------------------------

TEMPLATE = app
TARGET = CuteTorrent
DESTDIR = ../Win32/Release
QT += core gui network phonon gui-private
greaterThan(QT_MAJOR_VERSION, 4) {
  QT += widgets
  DEFINES += HAVE_QT5
}

CONFIG += release
DEFINES += QT_LARGEFILE_SUPPORT QT_DLL QT_PHONON_LIB
INCLUDEPATH += ./qtsingleapplication/src \
    ../../../../boost_1_53_0 \
    ../../../../libtorrent-rasterbar-0.16.8/include \
    ../build-CuteTorrent-Desktop-Release/GeneratedFiles \
    . \
    ../build-CuteTorrent-Desktop-Release/GeneratedFiles/Release
LIBS += -L"../../../../libtorrent-rasterbar-0.16.8/lib" \
    -L"../../../../boost_1_53_0/stage/lib" \


LIBS += -llibtorrent.x64 \



DEPENDPATH += .
MOC_DIR += ../build-CuteTorrent-Desktop-Release/GeneratedFiles/Release
OBJECTS_DIR += Release
UI_DIR += ./GeneratedFiles
RCC_DIR += ./GeneratedFiles
include(CuteTorrent.pri)
TRANSLATIONS += ./cutetorrent_english.ts \
    ./cutetorrent_russian.ts
QMAKE_LFLAGS += /MANIFESTUAC:"level='requireAdministrator'"
QMAKE_MOC += -DBOOST_MPL_IF_HPP_INCLUDED
win32 {
    RC_FILE = CuteTorrent.rc
}

win32-msvc* {
    # Works when you build for Visual Studio already
    vsproj.spec = $$basename(QMAKESPEC)
} else {
    # Works when you're not building for Visual Studio (say, using mingw)
    # The configuration you want to build the VS project for (win32-msvc[2005|2008|2010|2012])
    vsproj.spec = win32-msvc2008
}
# Sets the project file name appropriately to selected Visual Studio version.
contains(vsproj.spec, win32-msvc201): vsproj.target = $${TARGET}.vcxproj
else: vsproj.target = $${TARGET}.vcproj
# The qmake command to make the Visual Studio project file
vsproj.commands = qmake -tp vc $${_PRO_FILE_} -spec $${vsproj.spec}
# The VS project depends on the .pro file
vsproj.depends = $${_PRO_FILE_}
# Set the above as a target in the makefile.
QMAKE_EXTRA_TARGETS += vsproj
# Make the main target (the executable/library) depend on it,
# so that it gets built.
PRE_TARGETDEPS += $${vsproj.target}





